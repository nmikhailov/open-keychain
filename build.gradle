buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        // NOTE: Always use fixed version codes not dynamic ones, e.g. 0.7.3 instead of 0.7.+, see README for more information
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath files('gradle-witness.jar')
    }
}

allprojects {
    repositories {
        jcenter()

        maven {
            // for zxing-android-embedded lib
            url "http://dl.bintray.com/journeyapps/maven"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

subprojects {
    tasks.withType(Test) {
        maxParallelForks = 1
    }
}

// Ignore tests for external dependency
project(':extern:spongycastle') {
    subprojects {
        // Need to re-apply the plugin here otherwise the test property below can't be set.
        apply plugin: 'java'
        test.enabled = false
    }
}

// SDK Version and Build Tools used by all subprojects
// See http://tools.android.com/tech-docs/new-build-system/tips#TOC-Controlling-Android-properties-of-all-your-modules-from-the-main-project.
ext {
    compileSdkVersion = 22
    buildToolsVersion = '22.0.1'
}